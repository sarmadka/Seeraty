صنف مـشهد_المشاريع {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛
    عرف شاشة_عريضة: ثـنائي = 0؛
    عرف جديد: ثـنائي = 1؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق()؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_عريضة() {
        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = "flex"؛
                النسق = "column"؛
                ملء_السطر = "space-between"؛
                المحاذاة = "start"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الحشوة = ربـاعية(5)؛
            }؛
            الطراز(">>$img").{
                أقصى_عرض = "calc(100vw - 30pt)"؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = 14؛
            }؛
            أضف_فروع({
                كـتابة_متن(تـرجمة.نص(
                    "هذه قائمة المشاريع التي عملت عليها بشكل مستقل "
                    "ولا تشمل المشاريع التي أنجزتها أثناء عملي كموظف في شركة أخرى."
                ))،
                صـندوق().{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        العرض = "100%"؛
                        الطول = "calc(100% - 30pt)"؛
                    }؛
                    عرف كل_المشاريع: مـصفوفة[نـص] = البيانات.المشاريع.قطع("---")؛
                    عرف مشاريع1: مـصفوفة[نـص]؛
                    عرف مشاريع2: مـصفوفة[نـص]؛
                    عرف ع: صـحيح؛
                    لكل ع = 0، ع < كل_المشاريع.هات_الطول()، ++ع {
                        إذا ع % 2 == 0 مشاريع1.أضف(كل_المشاريع(ع))
                        وإلا مشاريع2.أضف(كل_المشاريع(ع))؛
                    }
                    أضف_فروع({
                        صـندوق().{
                            الطراز.{
                                العرض = "50%"؛
                                الطول = "100%"؛
                                الفيض = "scroll"؛
                            }؛
                            أضف_فروع({ عـمود_نصوص(مشاريع1، نـص("/projects")، 0) })؛
                        }،
                        صـندوق().{
                            الطراز.{
                                العرض = "50%"؛
                                الطول = "100%"؛
                                الفيض = "scroll"؛
                            }؛
                            أضف_فروع({ عـمود_نصوص(مشاريع2، نـص("/projects")، 0) })؛
                        }،
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_رفيعة() {
        هذا.المشهد = صـندوق().{
            الطراز.{
                ملء_السطر = "start"؛
                المحاذاة = "start"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الفيض = "scroll"؛
                الهامش = ربـاعية(5)؛
            }؛
            الطراز(">>$img").{
                أقصى_عرض = "calc(100vw - 30pt)"؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = 14؛
            }؛
            أضف_فروع({
                كـتابة_متن(تـرجمة.نص(
                    "هذه قائمة المشاريع التي عملت عليها بشكل مستقل "
                    "ولا تشمل المشاريع التي أنجزتها أثناء عملي كموظف في شركة أخرى."
                ))،
                عـمود_نصوص(البيانات.المشاريع، نـص("/projects")، 0)
            })؛
        }؛
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        إذا عرض == 0 ارجع؛
        عرف العرض_بالنقاط: صـحيح = عرض ÷ 1.3333؛
        عرف شاشة_عريضة: ثـنائي = العرض_بالنقاط > 550؛
        إذا هذا.جديد أو شاشة_عريضة != هذا.شاشة_عريضة {
            إذا شاشة_عريضة هذا.أنشئ_المشهد_لشاشة_عريضة()
            وإلا هذا.أنشئ_المشهد_لشاشة_رفيعة()؛
            هذا.جديد = 0؛
        }
        هذا.المشهد.الطراز.العرض = نـص.املأ("calc(%ipx - 30pt)"، عرض)؛
        هذا.شاشة_عريضة = شاشة_عريضة؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_المشاريع] {
        أرجع سـندنا[مـشهد_المشاريع]().{ احجز()~هيئ() }؛
    }
}
