صنف مـشهد_الخبرة_المهنية {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛
    عرف بيانات_الخط_الزمني: مـصفوفة[نـقطة_زمنية] = هذا.هات_النقاط_الزمنية(البيانات.الخبرة_المهنية)؛
    عرف شاشة_عريضة: ثـنائي = 0؛
    عرف جديد: ثـنائي = 1؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق()؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_عريضة() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                العرض = مـسافة.عن(100) - مـسافة.نقاط(30)؛
                الطول = مـسافة.مئوي(100) - مـسافة.نقاط(60)؛
                الهامش = مـسافة4.نقاط(5)؛
            }؛
            أضف_فروع({
                صـندوق().{
                    الطراز.{
                        العرض = مـسافة.نقاط(250)؛
                        الفيض_ص = فـيض._تمرير_؛
                        الهامش = مـسافة4.نقاط(5)؛
                    }؛
                    أضف_فروع({ مـشهد_الخط_الزمني(الكائن.بيانات_الخط_الزمني) })؛
                }،
                صـندوق().{
                    الطراز.{
                        المرونة = مـرونة(1)؛
                        الفيض_ص = فـيض._تمرير_؛
                        الهامش = مـسافة4.نقاط(5)؛
                    }؛
                    أضف_فروع({ عـمود_نصوص(البيانات.الخبرة_المهنية، هات_مسارا("/jobs/details")، 0) })؛
                }
            })؛
        }؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_رفيعة() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = مـبدال_توجيه().{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                العرض = مـسافة.عن(100) - مـسافة.نقاط(30)؛
                الطول = مـسافة.مئوي(100) - مـسافة.نقاط(60)؛
                الهامش = مـسافة4.نقاط(5)؛
                الفيض_ص = فـيض._مخفي_؛
            }؛
            حدد_الانتقال(
                أنشئ_انتقال_انزلاق_مبدال(0.5, 0، من_اليمين),
                أنشئ_انتقال_انزلاق_مبدال(0.5, 0، ليس من_اليمين)
            )؛
            أيجب_الاحتفاظ_بالفرع = مغلفة (سند[ودجـة]): ثـنائي { أرجع 1؛ }؛
            عرف عمود_الخط_الزمني: سـندنا[صـندوق] = صـندوق().{
                الطراز.{
                    الفيض_ص = فـيض._تمرير_؛
                    الطول = مـسافة.مئوي(100)؛
                    الهامش = مـسافة4.نقاط(5)؛
                }؛
                أضف_فروع({ مـشهد_الخط_الزمني(الكائن.بيانات_الخط_الزمني) })؛
            }؛
            عرف عمود_التفاصيل: سـندنا[صـندوق] = صـندوق().{
                الطراز.{
                    الإظهار = إظـهار._مرن_؛
                    النسق = نـسق._عمود_؛
                    الطول = مـسافة.مئوي(100)؛
                }؛
                أضف_فروع({
                    ارتـباط_تشعبي().{
                        العنوان = هات_مسارا("/jobs/timeline")؛
                        حدد_الفرع(كـتابة().{
                            النص = تـرجمة.نص("< الخط الزمني")؛
                            الطراز.{
                                سمك_الخط = سـمك_خط._سميك_؛
                            }؛
                        })؛
                        عند_الضغط.المبدئي_مكبوح = 1؛
                        عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                            نـافذة.النموذج.ادفع_مسارا(هات_مسارا("/jobs/timeline"))؛
                        })؛
                    }،
                    صـندوق().{
                        الطراز.{
                            الفيض_ص = فـيض._تمرير_؛
                            الهامش = مـسافة4.نقاط(5، 0، 5، 0)؛
                        }؛
                        أضف_فروع({ عـمود_نصوص(البيانات.الخبرة_المهنية، هات_مسارا("/jobs/details")، 500000) })؛
                    }
                })؛
            }؛
            مسار("^/(ar|en)/jobs/timeline$") = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                أرجع عمود_الخط_الزمني؛
            }؛
            مسار("^/(ar|en)/jobs/details$") = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                أرجع عمود_التفاصيل؛
            }؛
        }؛
    }

    عملية هذا.هات_النقاط_الزمنية(نصوص: مـصفوفة[نـص]): مـصفوفة[نـقطة_زمنية] {
        عرف ناتج: مـصفوفة[نـقطة_زمنية]؛
        عرف نمط: تـعبير_نمطي("<h2>(من|From) ([0-9]+)-([0-9]+) (إلى|To) ([^:]+): ([^/]+) / ([^/]+) / ([^/\n<]+)")؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < نصوص.هات_الطول()، ++ع {
            عرف تطابقات: مـصفوفة[نـص] = نمط.طابق(نصوص(ع))؛
            إذا تطابقات.هات_الطول() == 0 أكمل؛
            إذا تطابقات(5) == "الآن" أو تطابقات(5) == "Present" {
                ناتج.أضف(نـقطة_زمنية(
                    اقرأ_رقمًا(تطابقات(2))،
                    اقرأ_رقمًا(تطابقات(3))،
                    0،
                    0،
                    تطابقات(6) + "\ج" + تطابقات(7) + " / " + تطابقات(8)
                ))؛
            } وإلا {
                عرف إلى: مـصفوفة[نـص] = تطابقات(5).قطع("-")؛
                ناتج.أضف(نـقطة_زمنية(
                    اقرأ_رقمًا(تطابقات(2))،
                    اقرأ_رقمًا(تطابقات(3))،
                    اقرأ_رقمًا(إلى(0))،
                    اقرأ_رقمًا(إلى(1))،
                    تطابقات(6) + "\ج" + تطابقات(7) + " / " + تطابقات(8)
                ))؛
            }
        }
        أرجع ناتج؛
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        إذا عرض == 0 ارجع؛
        عرف العرض_بالنقاط: صـحيح = عرض ÷ 1.3333؛
        عرف شاشة_عريضة: ثـنائي = العرض_بالنقاط > 550؛
        إذا هذا.جديد أو شاشة_عريضة != هذا.شاشة_عريضة {
            إذا شاشة_عريضة هذا.أنشئ_المشهد_لشاشة_عريضة()
            وإلا هذا.أنشئ_المشهد_لشاشة_رفيعة()؛
            هذا.جديد = 0؛
        }
        هذا.المشهد.الطراز.العرض = مـسافة.بكسلات(عرض) - مـسافة.نقاط(30)؛
        هذا.شاشة_عريضة = شاشة_عريضة؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_الخبرة_المهنية] {
        أرجع سـندنا[مـشهد_الخبرة_المهنية]().{ احجز()~هيئ() }؛
    }
}
