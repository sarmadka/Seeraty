صنف مـشهد_المعلومات {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛
    عرف شاشة_عريضة: ثـنائي = 0؛
    عرف جديد: ثـنائي = 1؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق()؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_عريضة() {
        عرف معلومات1: مـصفوفة[نـص]({ البيانات.المعلومات(0)، البيانات.المعلومات(2) })؛
        عرف معلومات2: مـصفوفة[نـص]({ البيانات.المعلومات(1)، البيانات.المعلومات(3)، البيانات.المعلومات(4) })؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                ملء_السطر = مـلء_سطر._بداية_؛
                المحاذاة = مـحاذاة._بداية_؛
                العرض = مـسافة.عن(100) - مـسافة.نقاط(30)؛
                الطول = مـسافة.مئوي(100) - مـسافة.نقاط(60)؛
                الفيض_ص = فـيض._تمرير_؛
                الهامش = مـسافة4.نقاط(5)؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = مـسافة.نقاط(14)؛
            }؛
            أضف_فروع({
                صـندوق().{
                    الطراز.العرض = مـسافة.مئوي(50)؛
                    أضف_معلومات(هذا، معلومات1)؛
                }،
                صـندوق().{
                    الطراز.العرض = مـسافة.مئوي(50)؛
                    أضف_معلومات(هذا، معلومات2)؛
                }،
            })؛
        }؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_رفيعة() {
        هذا.المشهد = صـندوق().{
            الطراز.{
                ملء_السطر = مـلء_سطر._بداية_؛
                المحاذاة = مـحاذاة._بداية_؛
                العرض = مـسافة.عن(100) - مـسافة.نقاط(30)؛
                الطول = مـسافة.مئوي(100) - مـسافة.نقاط(60)؛
                الفيض_ص = فـيض._تمرير_؛
                الهامش = مـسافة4.نقاط(5)؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = مـسافة.نقاط(14)؛
            }؛
            أضف_معلومات(هذا، البيانات.المعلومات)؛
        }؛
    }

    دالة أضف_معلومات(صندوق: سند[صـندوق]، معلومات: مـصفوفة[نـص]) {
        عرف ع: صـحيح؛
        لكل ع = 0، ع < معلومات.هات_الطول()، ++ع {
            صندوق.أضف_فروع({
                مـشهد_وثيقة().{
                    الطراز.{
                        العرض = مـسافة.مئوي(100) - مـسافة.نقاط(24)؛
                        الخلفية = خـلفية(السمة.لون_خلفية_الفقرة)؛
                        نصف_قطر_الإطار = مـسافة4.نقاط(5)؛
                        الهامش = مـسافة4.نقاط(5)؛
                        الحشوة = مـسافة4.نقاط(0، 7، 2، 7)؛
                    }؛
                    حدد_لتنف(معلومات(ع))؛
                }
            })؛
        }
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        إذا عرض == 0 ارجع؛
        عرف العرض_بالنقاط: صـحيح = عرض ÷ 1.3333؛
        عرف شاشة_عريضة: ثـنائي = العرض_بالنقاط > 550؛
        إذا هذا.جديد أو شاشة_عريضة != هذا.شاشة_عريضة {
            إذا شاشة_عريضة هذا.أنشئ_المشهد_لشاشة_عريضة()
            وإلا هذا.أنشئ_المشهد_لشاشة_رفيعة()؛
            هذا.جديد = 0؛
        }
        هذا.المشهد.الطراز.العرض = مـسافة.بكسلات(عرض) - مـسافة.نقاط(30)؛
        هذا.شاشة_عريضة = شاشة_عريضة؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_المعلومات] {
        أرجع سـندنا[مـشهد_المعلومات]().{ احجز()~هيئ() }؛
    }
}
