صنف مـشهد_المعلومات {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛
    عرف شاشة_عريضة: ثـنائي = 0؛
    عرف جديد: ثـنائي = 1؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق()؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_عريضة() {
        عرف معلومات: مـصفوفة[نـص] = البيانات.المعلومات.قطع("##")؛
        عرف معلومات1: مـصفوفة[نـص]({ معلومات(1)، معلومات(3) })؛
        عرف معلومات2: مـصفوفة[نـص]({ معلومات(2)، معلومات(4)، معلومات(5) })؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = "flex"؛
                النسق = "row"؛
                ملء_السطر = "start"؛
                المحاذاة = "start"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الفيض = "scroll"؛
                الهامش = ربـاعية(5)؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = 14؛
            }؛
            أضف_فروع({
                صـندوق().{
                    الطراز.العرض = "50%"؛
                    أضف_معلومات(هذا، معلومات1)؛
                }،
                صـندوق().{
                    الطراز.العرض = "50%"؛
                    أضف_معلومات(هذا، معلومات2)؛
                }،
            })؛
        }؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_رفيعة() {
        عرف معلومات: مـصفوفة[نـص] = البيانات.المعلومات.قطع("##")؛
        معلومات.أزل(0)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                ملء_السطر = "start"؛
                المحاذاة = "start"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الفيض = "scroll"؛
                الهامش = ربـاعية(5)؛
            }؛
            الطراز(">>$h2").{
                حجم_الخط = 14؛
            }؛
            أضف_معلومات(هذا، معلومات)؛
        }؛
    }

    دالة أضف_معلومات(صندوق: سند[صـندوق]، معلومات: مـصفوفة[نـص]) {
        عرف ع: صـحيح؛
        لكل ع = 0، ع < معلومات.هات_الطول()، ++ع {
            صندوق.أضف_فروع({
                مـشهد_وثيقة().{
                    الطراز.{
                        العرض = "calc(100% - 24pt)"؛
                        الخلفية = السمة.لون_خلفية_الفقرة؛
                        نصف_قطر_الإطار = ربـاعية(5)؛
                        الهامش = ربـاعية(5)؛
                        الحشوة = ربـاعية(0، 7، 2، 7)؛
                    }؛
                    حدد_ماركداون(نـص("##") + معلومات(ع)، 1)؛
                }
            })؛
        }
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        إذا عرض == 0 ارجع؛
        عرف العرض_بالنقاط: صـحيح = عرض ÷ 1.3333؛
        عرف شاشة_عريضة: ثـنائي = العرض_بالنقاط > 550؛
        إذا هذا.جديد أو شاشة_عريضة != هذا.شاشة_عريضة {
            إذا شاشة_عريضة هذا.أنشئ_المشهد_لشاشة_عريضة()
            وإلا هذا.أنشئ_المشهد_لشاشة_رفيعة()؛
            هذا.جديد = 0؛
        }
        هذا.المشهد.الطراز.العرض = نـص.املأ("calc(%ipx - 30pt)"، عرض)؛
        هذا.شاشة_عريضة = شاشة_عريضة؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_المعلومات] {
        أرجع سـندنا[مـشهد_المعلومات]().{ احجز()~هيئ() }؛
    }
}
