صنف مـشهد_الخط_الزمني {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(بيانات: مـصفوفة[نـقطة_زمنية]) {
        عرف ألوان: مـصفوفة[نـص]({
            نـص("888")،
            نـص("8ff")،
            نـص("f8f")،
            نـص("ff8")،
        })؛
        هذا.المشهد = صـندوق().{
            عرف ع: صـحيح؛
            لكل ع = 0، ع < بيانات.هات_الطول()، ++ع {
                عرف طول: صـحيح؛
                إذا ع > 0 و بيانات(ع).هات_البعد_عن(بيانات(ع - 1)) > 0 {
                    أضف_فروع({
                        صـندوق().{
                            الطراز.الطول = مـسافة.نقاط(بيانات(ع).هات_البعد_عن(بيانات(ع - 1)) * 4)؛
                        }
                    })؛
                }
                أضف_فروع({
                    صـندوق().{
                        الطراز.{
                            الإظهار = إظـهار._مرن_؛
                            النسق = نـسق._سطر_؛
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.نقاط(الأعلى[صـحيح](بيانات(ع).هات_الطول() * 4، 60))؛
                        }؛
                        أضف_فروع({
                            صـندوق().{
                                الطراز.{
                                    الطول = مـسافة.مئوي(100)؛
                                    العرض = مـسافة.نقاط(10)؛
                                    الخلفية = خـلفية(لـون(ألوان(ع%4)))؛
                                    سمك_الإطار = مـسافة4.نقاط(2، 0، 2، 0)؛
                                    لون_الإطار = لـون("000")؛
                                    طراز_الإطار = طـراز_إطار._مستمر_؛
                                }؛
                            }،
                            صـندوق().{
                                الطراز.{
                                    الإظهار = إظـهار._مرن_؛
                                    النسق = نـسق._عمود_؛
                                    ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                                    الحشوة = مـسافة4.نقاط(0، 5، 0، 5)؛
                                }؛
                                أضف_فروع({
                                    كـتابة().{
                                        النص = نـص.املأ("%i-%i"، بيانات(ع).إلى_سنة، بيانات(ع).إلى_شهر)؛
                                        الطراز.حجم_الخط = مـسافة.نقاط(8)؛
                                    }،
                                    كـتابة().{
                                        النص = بيانات(ع).وصف؛
                                        الطراز.{
                                            حجم_الخط = مـسافة.نقاط(10)؛
                                            لون_الخط = السمة.لون_الرابط؛
                                            المؤشر = مـؤشر._سبابة_؛
                                        }؛
                                        عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                                            نـافذة.النموذج.ادفع_مسارا(نـص("/jobs/details#") + ع)؛
                                        })؛
                                    }،
                                    كـتابة().{
                                        النص = نـص.املأ("%i-%i"، بيانات(ع).من_سنة، بيانات(ع).من_شهر)؛
                                        الطراز.حجم_الخط = مـسافة.نقاط(8)؛
                                    }،
                                })؛
                            }
                        })؛
                    }
                })؛
            }
        }؛
    }

    عملية هذا_الصنف (بيانات: مـصفوفة[نـقطة_زمنية]): سـندنا[مـشهد_الخط_الزمني] {
        أرجع سـندنا[مـشهد_الخط_الزمني]().{ احجز()~هيئ(بيانات) }؛
    }
}

صنف نـقطة_زمنية {
    عرف من_سنة: صـحيح؛
    عرف من_شهر: صـحيح؛
    عرف إلى_سنة: صـحيح؛
    عرف إلى_شهر: صـحيح؛
    عرف وصف: نـص؛
    عملية هذا~هيئ() {}
    عملية هذا~هيئ(من_سنة: صـحيح، من_شهر: صـحيح، إلى_سنة: صـحيح، إلى_شهر: صـحيح، وصف: نـص) {
        هذا.من_سنة = من_سنة؛
        هذا.من_شهر = من_شهر؛
        هذا.إلى_سنة = إلى_سنة؛
        هذا.إلى_شهر = إلى_شهر؛
        هذا.وصف = وصف؛
    }
    عملية هذا~هيئ(سند[هذا_الصنف]) هذا = قيمة؛
    عملية هذا = سند[هذا_الصنف] {
        هذا.من_سنة = قيمة.من_سنة؛
        هذا.من_شهر = قيمة.من_شهر؛
        هذا.إلى_سنة = قيمة.إلى_سنة؛
        هذا.إلى_شهر = قيمة.إلى_شهر؛
        هذا.وصف = قيمة.وصف؛
    }
    عملية هذا.هات_الطول(): صـحيح {
        أرجع هذا.إلى_سنة * 12 + هذا.إلى_شهر - هذا.من_سنة * 12 - هذا.من_شهر؛
    }
    عملية هذا.هات_البعد_عن(سند[هذا_الصنف]): صـحيح {
        أرجع قيمة.من_سنة * 12 + قيمة.من_شهر - هذا.إلى_سنة * 12 - هذا.إلى_شهر؛
    }
}
