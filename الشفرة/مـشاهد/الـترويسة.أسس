صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({ }).{
            الطراز.{
                العرض = "calc(100% - 20pt)"؛
                الحشوة = ربـاعية(10، 10, 20, 10)؛
                ملء_السطر = "space-between"؛
                المحاذاة = "start"؛
                الإظهار = "flex"؛
                النسق = "column"؛
            }؛
            أضف_فروع({
                صـندوق({ }).{
                    الطراز.{
                        العرض = "100%"؛
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "space-between"؛
                        المحاذاة = "start"؛
                    }؛
                    أضف_فروع({
                        مـشهد_وثيقة().{
                            الطراز(">>$h2").{
                                حجم_الخط = 18؛
                                ارتفاع_الكتابة = 0.5؛
                            }؛
                            حدد_ماركداون(البيانات.تعريف، 1)؛
                        }،
                        صـندوق().{
                            الطراز.{
                                الإظهار = "flex"؛
                                النسق = "column"؛
                                المحاذاة = "end"؛
                                ملء_السطر = "start"؛
                            }؛
                            أضف_فروع({
                                ارتـباط_تشعبي().{
                                    العنوان = نـص("/resume/sarmad-abdullah-resume.pdf")؛
                                    لسان_جديد = 1؛
                                    حدد_الفرع(صـورة().{
                                        الطراز.العرض = "30pt"؛
                                        الرابط = نـص("/الموارد/pdf.svg")؛
                                    })؛
                                }،
                                كـتابة().{
                                    الطراز.{
                                        المؤشر = "pointer"؛
                                        لون_الخط = السمة.لون_الرابط؛
                                        سمك_الإطار = ربـاعية(2)؛
                                        لون_الإطار = السمة.لون_الرابط؛
                                        طراز_الإطار = "solid"؛
                                        الحشوة = ربـاعية(5)؛
                                        الهامش = ربـاعية(5)؛
                                        نصف_قطر_الإطار = ربـاعية(4)؛
                                    }؛
                                    إذا اللغة == "ar" النص = نـص("E") وإلا النص = نـص("ع")؛
                                    عند_الضغط.اربط(مغلفة (سند[ودجـة]، سند[صـحيح]) {
                                        إذا اللغة == "ar" نـافذة.النموذج.المخزن_المحلي.حدد_عنصرا("اللغة"، "en")
                                        وإلا نـافذة.النموذج.المخزن_المحلي.حدد_عنصرا("اللغة"، "ar")؛
                                        أعد_التوجيه("/")؛
                                    })؛
                                }
                            })؛
                        }
                    })؛
                },
            })؛
        }؛
    }

    عملية هذا_الصنف (): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

