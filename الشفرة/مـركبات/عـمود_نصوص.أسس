صنف عـمود_نصوص {
    @حقنة عرف مركب: مـركب؛
    عرف المسار: نـص؛
    عرف معالج_تغير_المسار: مغلفة (سند[نـافذة], سند[صـحيح]) = مغلفة (سند[نـافذة], سند[صـحيح]) {
        هذا.عند_تغير_المسار()؛
    };

    عملية هذا~هيئ(نصوص: نـص، مسار: نـص) {
        هذا.المسار = مسار؛
        نـافذة.النموذج.عند_تغير_المسار.اربط(هذا.معالج_تغير_المسار)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                ملء_السطر = "start"؛
                المحاذاة = "start"؛
                العرض = "100%"؛
            }؛
            الطراز(">>#img").{
                أقصى_عرض = "100%"؛
            }؛
            الطراز(">>#h2").{
                حجم_الخط = 14؛
            }؛
            عرف بيانات: مـصفوفة[نـص] = نـص(نصوص).قطع("---")؛
            عرف ع: صـحيح؛
            لكل ع = 0، ع < بيانات.هات_الطول()، ++ع {
                أضف_فروع({
                    قـابل_للضغط(مـشهد_وثيقة().{
                        حدد_ماركداون(بيانات(ع))؛
                    })
                })؛
            }
        }؛
    }

    عملية هذا~أتلف() {
        نـافذة.النموذج.عند_تغير_المسار.افصل(هذا.معالج_تغير_المسار)؛
    }

    عملية هذا.عند_تغير_المسار() {
        إذا نـافذة.النموذج.المسار != هذا.المسار ارجع؛
        عرف ر: صحيح = اقرأ_رقمًا(نـافذة.النموذج.الوسم.صوان + 1)؛
        عرف صندوق: سند[صـندوق](هذا.المشهد.كائن~مثل[سند[صـندوق]])؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < صندوق.فروع.هات_الطول()، ++ع {
            إذا ر == ع {
                صندوق.فروع(ع).مرر_إلى_المرأى()؛
                صندوق.فروع(ع).كائن~مثل[سند[قـابل_للضغط]].غير_الحالة(1، 1)؛
            } وإلا {
                عرف قض: سند[قـابل_للضغط](صندوق.فروع(ع).كائن~مثل[سند[قـابل_للضغط]])؛
                قض.غير_الحالة(قض.مفتوح، 0)؛
            }
        }
    }

    عملية (هذا:مـركب).تذيل_البناء() حدد_مؤشر {
        هذا.عند_تغير_المسار()؛
    }

    عملية هذا_الصنف (نصوص: نـص، مسار: نـص): سـندنا[عـمود_نصوص] {
        أرجع سـندنا[عـمود_نصوص]().{ احجز()~هيئ(نصوص، مسار) }؛
    }
}
