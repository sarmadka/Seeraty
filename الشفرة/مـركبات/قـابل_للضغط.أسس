صنف قـابل_للضغط {
    @حقنة عرف مركب: مـركب؛
    عرف مفتوح: ثـنائي = 0؛
    عرف معلم: ثـنائي = 0؛

    عملية هذا~هيئ(مشهد: سـندنا[ودجـة]) {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                العرض = "calc(100% - 10pt)"؛
                الطول = "130pt"؛
                الانتقال = "width 0.5s, height 0.5s, border-radius 0.5s, top 0.5s, right 0.5s, left 0.5s"؛
                الفيض = "hidden"؛
                الموقع = "relative"؛
                الخلفية = السمة.لون_خلفية_الفقرة؛
                الهامش = ربـاعية(10، 5، 10، 5)؛
                نصف_قطر_الإطار = ربـاعية(5)؛
            }؛
            الطراز("مفتوح").{
                الطول = "100pt"؛
            }؛
            الطراز("معلم").{
                الخلفية = السمة.لون_خلفية_الفقرة_المختارة؛
            }؛
            الطراز(">مزيد").{
                الحشوة = ربـاعية(10، 0، 0، 0)؛
                الخلفية = نـص.املأ("linear-gradient(180deg, %s, %s 30%%)",
                    لـون(السمة.لون_خلفية_الفقرة).{ شفافية = 0 }~مثل[نـص].صوان,
                    لـون(السمة.لون_خلفية_الفقرة).{ شفافية = 255 }~مثل[نـص].صوان,
                )؛
                العرض = "100%"؛
                الطول = "20pt"؛
                الموقع = "absolute"؛
                الإظهار = "flex"؛
                النسق = "column"؛
                المحاذاة = "center"؛
                تحت = "0"؛
            }؛
            الطراز({ "معلم"، ">مزيد" }).{
                الخلفية = نـص.املأ("linear-gradient(180deg, %s, %s 30%%)",
                    لـون(السمة.لون_خلفية_الفقرة_المختارة).{ شفافية = 0 }~مثل[نـص].صوان,
                    لـون(السمة.لون_خلفية_الفقرة_المختارة).{ شفافية = 255 }~مثل[نـص].صوان,
                )؛
            }؛
            الطراز({ ">مزيد"، ">>للأسفل" }).{
                الطول = "20pt"؛
            }؛
            الطراز({ ">مزيد"، ">>للأعلى" }).{
                الطول = "20pt"؛
            }؛
            الطراز({ "مغلق"، ">مزيد"، ">>للأعلى" }).{
                الإظهار = "none"؛
            }؛
            الطراز({ "مفتوح"، ">مزيد"، ">>للأسفل" }).{
                الإظهار = "none"؛
            }؛
            الطراز(">داخلي").{
                الفيض = "auto"؛
                الحشوة = "0pt 10pt 20pt 10pt"؛
            }؛
            اسم_الفئة = نـص("مغلق")؛
            أضف_فروع({
                مشهد.{
                    اسم_الفئة = نـص("داخلي")؛
                    عند_تغير_الأبعاد.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        الكائن.المشهد.الطراز("مفتوح").الطول = نـص.املأ("%ipx"، هذا.هات_الأبعاد().طول)؛
                    })؛
                }،
                صـندوق().{
                    اسم_الفئة = نـص("مزيد")؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        الكائن.غير_الحالة(ليس الكائن.مفتوح، الكائن.معلم)؛
                    })؛
                    أضف_فروع({
                        صـورة(السمة.زر_للأسفل).{
                            اسم_الفئة = نـص("للأسفل")؛
                        }،
                        صـورة(السمة.زر_للأعلى).{
                            اسم_الفئة = نـص("للأعلى")؛
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا.غير_الحالة(مفتوح: ثـنائي، معلم: ثـنائي) {
        إذا مفتوح و معلم هذا.المشهد.اسم_الفئة = نـص("مفتوح معلم")
        وإلا إذا مفتوح هذا.المشهد.اسم_الفئة = نـص("مفتوح")
        وإلا إذا معلم هذا.المشهد.اسم_الفئة = نـص("مغلق معلم")
        وإلا هذا.المشهد.اسم_الفئة = نـص("مغلق")؛
        هذا.مفتوح = مفتوح؛
        هذا.معلم = معلم؛
    }

    عملية هذا_الصنف (مشهد: سـندنا[ودجـة]): سـندنا[قـابل_للضغط] {
        أرجع سـندنا[قـابل_للضغط]().{ احجز()~هيئ(مشهد) }؛
    }
}
