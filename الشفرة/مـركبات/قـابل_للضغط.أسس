صنف قـابل_للضغط {
    @حقنة عرف مركب: مـركب؛
    عرف مفتوح: ثـنائي = 0؛

    عملية هذا~هيئ(مشهد: سـندنا[ودجـة]) {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                العرض = "100%"؛
                الطول = "130pt"؛
                الانتقال = "width 0.5s, height 0.5s, border-radius 0.5s, top 0.5s, right 0.5s, left 0.5s"؛
                الفيض = "hidden"؛
                الموقع = "relative"؛
            }؛
            الطراز("مفتوح").{
                الطول = "100pt"؛
            }؛
            الطراز(">مزيد").{
                الحشوة = ربـاعية(10، 0، 0، 0)؛
                الخلفية = "linear-gradient(180deg, #fff0, #ffff 30%)"؛
                العرض = "100%"؛
                الطول = "20pt"؛
                الموقع = "absolute"؛
                الإظهار = "flex"؛
                النسق = "column"؛
                المحاذاة = "center"؛
                تحت = "0"؛
            }؛
            الطراز({ ">مزيد"، ">>للأسفل" }).{
                الطول = "20pt"؛
            }؛
            الطراز({ ">مزيد"، ">>للأعلى" }).{
                الطول = "20pt"؛
            }؛
            الطراز({ "مغلق"، ">مزيد"، ">>للأعلى" }).{
                الإظهار = "none"؛
            }؛
            الطراز({ "مفتوح"، ">مزيد"، ">>للأسفل" }).{
                الإظهار = "none"؛
            }؛
            الطراز(">داخلي").{
                الفيض = "auto"؛
                الحشوة = "0pt 0pt 20pt 0pt"؛
            }؛
            اسم_الفئة = نـص("مغلق")؛
            أضف_فروع({
                مشهد.{
                    اسم_الفئة = نـص("داخلي")؛
                    عند_تغير_الأبعاد.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        الكائن.المشهد.الطراز("مفتوح").الطول = نـص.املأ("%ipx"، هذا.هات_الأبعاد().طول)؛
                    })؛
                }،
                صـندوق().{
                    اسم_الفئة = نـص("مزيد")؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        إذا الكائن.مفتوح الكائن.المشهد.اسم_الفئة = نـص("مغلق")
                        وإلا الكائن.المشهد.اسم_الفئة = نـص("مفتوح")؛
                        الكائن.مفتوح = ليس الكائن.مفتوح؛
                    })؛
                    أضف_فروع({
                        صـورة(نـص("/الموارد/للأسفل.svg")).{
                            اسم_الفئة = نـص("للأسفل")؛
                        }،
                        صـورة(نـص("/الموارد/للأعلى.svg")).{
                            اسم_الفئة = نـص("للأعلى")؛
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف (مشهد: سـندنا[ودجـة]): سـندنا[قـابل_للضغط] {
        أرجع سـندنا[قـابل_للضغط]().{ احجز()~هيئ(مشهد) }؛
    }
}
