صنف قـابل_للضغط {
    @حقنة عرف مركب: مـركب؛
    عرف مفتوح: ثـنائي = 0؛
    عرف معلم: ثـنائي = 0؛

    عملية هذا~هيئ(مشهد: سـندنا[ودجـة]) {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                العرض = مـسافة.مئوي(100) - مـسافة.نقاط(10)؛
                الطول = مـسافة.نقاط(130)؛
                انتقال_الطول = انـتقال(0.5)؛
                الفيض_ص = فـيض._مخفي_؛
                الموقع = مـوقع._نسبي_؛
                الخلفية = خـلفية(السمة.لون_خلفية_الفقرة)؛
                الهامش = مـسافة4.نقاط(10، 5، 10، 5)؛
                نصف_قطر_الإطار = مـسافة4.نقاط(5)؛
            }؛
            الطراز("مفتوح").{
                الطول = مـسافة.نقاط(100)؛
            }؛
            الطراز("معلم").{
                الخلفية = خـلفية(السمة.لون_خلفية_الفقرة_المختارة)؛
            }؛
            الطراز(">مزيد").{
                الحشوة = مـسافة4.نقاط(10، 0، 0، 0)؛
                الخلفية = خـلفية(180، {
                    لـون(السمة.لون_خلفية_الفقرة).{ شفافية = 0 }, 0،
                    لـون(السمة.لون_خلفية_الفقرة).{ شفافية = 255 }، 30
                })؛
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(20)؛
                الموقع = مـوقع._مطلق_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
                المحاذاة = مـحاذاة._وسط_؛
                تحت = مـسافة.نقاط(0)؛
            }؛
            الطراز({ "معلم"، ">مزيد" }).{
                الخلفية = خـلفية(180، {
                    لـون(السمة.لون_خلفية_الفقرة_المختارة).{ شفافية = 0 }، 0،
                    لـون(السمة.لون_خلفية_الفقرة_المختارة).{ شفافية = 255 }، 30
                })؛
            }؛
            الطراز({ ">مزيد"، ">>للأسفل" }).{
                الطول = مـسافة.نقاط(20)؛
            }؛
            الطراز({ ">مزيد"، ">>للأعلى" }).{
                الطول = مـسافة.نقاط(20)؛
            }؛
            الطراز({ "مغلق"، ">مزيد"، ">>للأعلى" }).{
                الإظهار = إظـهار._مخفي_؛
            }؛
            الطراز({ "مفتوح"، ">مزيد"، ">>للأسفل" }).{
                الإظهار = إظـهار._مخفي_؛
            }؛
            الطراز(">داخلي").{
                الفيض_ص = فـيض._تلقائي_؛
                الحشوة = مـسافة4.نقاط(0، 10، 20، 10)؛
            }؛
            اسم_الفئة = نـص("مغلق")؛
            أضف_فروع({
                مشهد.{
                    اسم_الفئة = نـص("داخلي")؛
                    عند_تغير_الأبعاد.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        الكائن.المشهد.الطراز("مفتوح").الطول = مـسافة.بكسلات(هذا.هات_الأبعاد().طول)؛
                    })؛
                }،
                صـندوق().{
                    اسم_الفئة = نـص("مزيد")؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        الكائن.غير_الحالة(ليس الكائن.مفتوح، الكائن.معلم)؛
                    })؛
                    أضف_فروع({
                        صـورة(السمة.زر_للأسفل).{
                            اسم_الفئة = نـص("للأسفل")؛
                        }،
                        صـورة(السمة.زر_للأعلى).{
                            اسم_الفئة = نـص("للأعلى")؛
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا.غير_الحالة(مفتوح: ثـنائي، معلم: ثـنائي) {
        إذا مفتوح و معلم هذا.المشهد.اسم_الفئة = نـص("مفتوح معلم")
        وإلا إذا مفتوح هذا.المشهد.اسم_الفئة = نـص("مفتوح")
        وإلا إذا معلم هذا.المشهد.اسم_الفئة = نـص("مغلق معلم")
        وإلا هذا.المشهد.اسم_الفئة = نـص("مغلق")؛
        هذا.مفتوح = مفتوح؛
        هذا.معلم = معلم؛
    }

    عملية هذا_الصنف (مشهد: سـندنا[ودجـة]): سـندنا[قـابل_للضغط] {
        أرجع سـندنا[قـابل_للضغط]().{ احجز()~هيئ(مشهد) }؛
    }
}
