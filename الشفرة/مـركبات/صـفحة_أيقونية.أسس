صنف صـفحة_أيقونية {
    @حقنة عرف مركب: مـركب؛
    عرف المسار: نـص؛
    عرف مشهد_أيقوني: سـندنا[مـشهد_أيقوني]؛

    عملية هذا~هيئ(س: نـص، مسار: نـص، أيقونة: نـص، عنوان: نـص، مشهد: سـندنا[مـشهد_أيقوني]) {
        هذا.مشهد_أيقوني = مشهد؛
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المسار = مسار؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الموقع = "absolute"؛
                نصف_قطر_الإطار = "25pt"؛
                الفيض = "hidden"؛
                الخلفية = السمة.لون_خلفية_الصفحة؛
                الانتقال = "width 0.5s, height 0.5s, border-radius 0.5s, top 0.5s, right 0.5s, left 0.5s"؛
            }؛
            الطراز("مغلق").{
                فوق = "0pt"؛
                إذا من_اليمين يمين = س وإلا يسار = س؛
                العرض = "50pt"؛
                الطول = "50pt"؛
            }؛
            الطراز("مفتوح").{
                فوق = "55pt"؛
                إذا من_اليمين يمين = "10pt" وإلا يسار = "10pt"؛
                العرض = "calc(100% - 20pt)"؛
                الطول = "calc(100% - 65pt)"؛
                إذا من_اليمين نصف_قطر_الإطار = ربـاعية(0، 25، 0، 0)
                وإلا نصف_قطر_الإطار = ربـاعية(25، 0، 0، 0)؛
            }؛
            اسم_الفئة = نـص("مغلق")؛
            أضف_فروع({
                صـندوق().{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        المحاذاة = "center"؛
                        العرض = "250pt"؛
                    }؛
                    أضف_فروع({
                        صـورة().{
                            الرابط = أيقونة؛
                            الطراز.{
                                العرض = "46pt"؛
                                الطول = "46pt"؛
                                إذا من_اليمين الهامش = "2pt 2pt 2pt 10pt"
                                وإلا الهامش = "2pt 10pt 2pt 2pt"؛
                            }؛
                            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                                إذا نـص.قارن(نـافذة.النموذج.المسار، الكائن.المسار، الكائن.المسار.هات_الطول()) == 0 {
                                    نـافذة.النموذج.ادفع_مسارا("/")؛
                                } وإلا {
                                    نـافذة.النموذج.ادفع_مسارا(الكائن.المسار)؛
                                }
                            })؛
                        }،
                        كـتابة_عنوان(عنوان)
                    })؛
                }،
                مشهد
            })؛
        }؛
    }

    عملية هذا.قد_تغير_العرض(عرض: صـحيح) {
        هذا.مشهد_أيقوني.قد_تغير_العرض(عرض)؛
    }

    عملية هذا.افتح() {
        هذا.المشهد.اسم_الفئة = نـص("مفتوح")؛
    }

    عملية هذا.أغلق() {
        هذا.المشهد.اسم_الفئة = نـص("مغلق")؛
    }

    عملية هذا_الصنف(س: نـص، مسار: نـص، أيقونة: نـص، عنوان: نـص، مشهد: سـندنا[مـشهد_أيقوني]): سـندنا[صـفحة_أيقونية] {
        أرجع سـندنا[صـفحة_أيقونية]().{ احجز()~هيئ(س، مسار، أيقونة، عنوان، مشهد) }؛
    }
}
