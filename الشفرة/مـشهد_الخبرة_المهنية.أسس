صنف مـشهد_الخبرة_المهنية {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛
    عرف بيانات_الخبرة: نـص = نـص(سلسلة_محارف_من_ملف["الـبيانات/الخبرة_المهنية.نص"])؛
    عرف بيانات_الخط_الزمني: مـصفوفة[نـقطة_زمنية] = هذا.هات_النقاط_الزمنية(بيانات_الخبرة)؛
    عرف شاشة_عريضة: ثـنائي = 0؛
    عرف جديد: ثـنائي = 1؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق()؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_عريضة() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = "flex"؛
                النسق = "row"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الهامش = ربـاعية(5)؛
            }؛
            أضف_فروع({
                صـندوق().{
                    الطراز.{
                        العرض = "250pt"؛
                        الفيض = "scroll"؛
                        الهامش = ربـاعية(5)؛
                    }؛
                    أضف_فروع({ مـشهد_الخط_الزمني(الكائن.بيانات_الخط_الزمني) })؛
                }،
                صـندوق().{
                    الطراز.{
                        المرونة = "1"؛
                        الفيض = "scroll"؛
                        الهامش = ربـاعية(5)؛
                    }؛
                    أضف_فروع({ عـمود_نصوص(الكائن.بيانات_الخبرة، نـص("/jobs/details")) })؛
                }
            })؛
        }؛
    }

    عملية هذا.أنشئ_المشهد_لشاشة_رفيعة() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = مـبدال_توجيه().{
            الطراز.{
                الإظهار = "flex"؛
                النسق = "row"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الهامش = ربـاعية(5)؛
            }؛
            حدد_الانتقال(
                أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 1),
                أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 0)
            )؛
            مسار("^/jobs/timeline$") = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                أرجع صـندوق().{
                    الطراز.{
                        الفيض = "scroll"؛
                        الطول = "100%"؛
                        الهامش = ربـاعية(5)؛
                    }؛
                    أضف_فروع({ مـشهد_الخط_الزمني(الكائن.بيانات_الخط_الزمني) })؛
                }؛
            }؛
            مسار("^/jobs/details$") = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                أرجع صـندوق().{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "column"؛
                        الطول = "100%"؛
                    }؛
                    أضف_فروع({
                        كـتابة().{
                            النص = نـص("< الخط الزمني")؛
                            الطراز.{
                                وزن_الخط = "bold"؛
                                المؤشر = "pointer"؛
                            }؛
                            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                                نـافذة.النموذج.ادفع_مسارا(نـص("/jobs/timeline"))؛
                            })؛
                        }،
                        صـندوق().{
                            الطراز.{
                                الفيض = "scroll"؛
                                الهامش = ربـاعية(5)؛
                            }؛
                            أضف_فروع({ عـمود_نصوص(الكائن.بيانات_الخبرة، نـص("/jobs/details")) })؛
                        }
                    })؛
                }؛
            }؛
        }؛
    }

    عملية هذا.هات_النقاط_الزمنية(نص: نـص): مـصفوفة[نـقطة_زمنية] {
        عرف ناتج: مـصفوفة[نـقطة_زمنية]؛
        عرف نمط: تـعبير_نمطي("## (من|from) ([0-9]+)-([0-9]+) (إلى|to) ([0-9]+)-([0-9]+): ([^/]+) / ([^/]+) / ([^/\n]+)")؛
        عرف م: مـؤشر_محارف = نص.صوان؛
        بينما 1 {
            عرف موقع_المواصلة: صـحيح؛
            عرف تطابقات: مـصفوفة[نـص] = نمط.طابق(م، موقع_المواصلة)؛
            إذا تطابقات.هات_الطول() == 0 اقطع؛
            ناتج.أضف(نـقطة_زمنية(
                اقرأ_رقمًا(تطابقات(2))،
                اقرأ_رقمًا(تطابقات(3))،
                اقرأ_رقمًا(تطابقات(5))،
                اقرأ_رقمًا(تطابقات(6))،
                تطابقات(7) + "<br>" + تطابقات(8) + " / " + تطابقات(9)
            ))؛
            بينما موقع_المواصلة > 0 {
                م += 1؛
                بينما (م~محتوى(0) & 0xC0) == 0x80 { م += 1 }
                --موقع_المواصلة؛
            }
        }
        أرجع ناتج؛
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        إذا عرض == 0 ارجع؛
        عرف العرض_بالنقاط: صـحيح = عرض ÷ 1.3333؛
        عرف شاشة_عريضة: ثـنائي = العرض_بالنقاط > 550؛
        إذا هذا.جديد أو شاشة_عريضة != هذا.شاشة_عريضة {
            إذا شاشة_عريضة هذا.أنشئ_المشهد_لشاشة_عريضة()
            وإلا هذا.أنشئ_المشهد_لشاشة_رفيعة()؛
            هذا.جديد = 0؛
        }
        هذا.المشهد.الطراز.العرض = نـص.املأ("calc(%ipx - 30pt)"، عرض)؛
        هذا.شاشة_عريضة = شاشة_عريضة؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_الخبرة_المهنية] {
        أرجع سـندنا[مـشهد_الخبرة_المهنية]().{ احجز()~هيئ() }؛
    }
}
