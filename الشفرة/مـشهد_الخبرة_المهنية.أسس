صنف مـشهد_الخبرة_المهنية {
    @حقنة عرف مشهد_أيقوني: مـشهد_أيقوني؛

    عملية هذا~هيئ() {
        عرف بيانات_الخبرة: نـص = نـص(سلسلة_محارف_من_ملف["الـبيانات/الخبرة_المهنية.نص"])؛
        عرف بيانات_الخط_الزمني: مـصفوفة[نـقطة_زمنية] = هذا.هات_النقاط_الزمنية(بيانات_الخبرة)؛

        هذا.المشهد = صـندوق().{
            الطراز.{
                الإظهار = "flex"؛
                النسق = "row"؛
                العرض = "calc(100vw - 30pt)"؛
                الطول = "calc(100% - 60pt)"؛
                الهامش = ربـاعية(5)؛
            }؛
            أضف_فروع({
                صـندوق().{
                    الطراز.{
                        العرض = "250pt"؛
                        الفيض = "scroll"؛
                        الهامش = ربـاعية(5)؛
                    }؛
                    أضف_فروع({ مـشهد_الخط_الزمني(بيانات_الخط_الزمني) })؛
                }،
                صـندوق().{
                    الطراز.{
                        المرونة = "1"؛
                        الفيض = "scroll"؛
                        الهامش = ربـاعية(5)؛
                    }؛
                    أضف_فروع({ عـمود_نصوص(بيانات_الخبرة، نـص("/jobs")) })؛
                }
            })؛
        }؛
    }

    عملية هذا.هات_النقاط_الزمنية(نص: نـص): مـصفوفة[نـقطة_زمنية] {
        عرف ناتج: مـصفوفة[نـقطة_زمنية]؛
        عرف نمط: تـعبير_نمطي("## (من|from) ([0-9]+)-([0-9]+) (إلى|to) ([0-9]+)-([0-9]+): ([^/]+) / ([^/]+) / ([^/\n]+)")؛
        عرف م: مـؤشر_محارف = نص.صوان؛
        بينما 1 {
            عرف موقع_المواصلة: صـحيح؛
            عرف تطابقات: مـصفوفة[نـص] = نمط.طابق(م، موقع_المواصلة)؛
            إذا تطابقات.هات_الطول() == 0 اقطع؛
            ناتج.أضف(نـقطة_زمنية(
                اقرأ_رقمًا(تطابقات(2))،
                اقرأ_رقمًا(تطابقات(3))،
                اقرأ_رقمًا(تطابقات(5))،
                اقرأ_رقمًا(تطابقات(6))،
                تطابقات(7) + "<br>" + تطابقات(8) + " / " + تطابقات(9)
            ))؛
            بينما موقع_المواصلة > 0 {
                م += 1؛
                بينما (م~محتوى(0) & 0xC0) == 0x80 { م += 1 }
                --موقع_المواصلة؛
            }
        }
        أرجع ناتج؛
    }

    عملية هذا.هات_النقاط_الزمنية2(نص: نـص): مـصفوفة[نـقطة_زمنية] {
        عرف ناتج: مـصفوفة[نـقطة_زمنية]؛
        عرف نمط: تـعبير_نمطي("## (من|from) ([0-9]+)-([0-9]+) (إلى|to) ([0-9]+)-([0-9]+): ([^/]+) / ([^/]+) / ([^/\n]+)")؛
        عرف مقاطع: مـصفوفة[نـص] = نص.قطع("---")؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < مقاطع.هات_الطول()، ++ع {
            عرف تطابقات: مـصفوفة[نـص] = نمط.طابق(مقاطع(ع))؛
            إذا تطابقات.هات_الطول() == 0 اقطع؛
            ناتج.أضف(نـقطة_زمنية(
                اقرأ_رقمًا(تطابقات(2))،
                اقرأ_رقمًا(تطابقات(3))،
                اقرأ_رقمًا(تطابقات(5))،
                اقرأ_رقمًا(تطابقات(6))،
                تطابقات(7) + "<br>" + تطابقات(8) + " / " + تطابقات(9)
            ))؛
        }
       أرجع ناتج؛
    }

    عملية (هذا: مـشهد_أيقوني).قد_تغير_العرض(عرض: صـحيح) حدد_مؤشر {
        هذا.المشهد.الطراز.العرض = نـص.املأ("calc(%ipx - 30pt)"، عرض)؛
    }

    عملية هذا_الصنف (): سـندنا[مـشهد_الخبرة_المهنية] {
        أرجع سـندنا[مـشهد_الخبرة_المهنية]().{ احجز()~هيئ() }؛
    }
}
