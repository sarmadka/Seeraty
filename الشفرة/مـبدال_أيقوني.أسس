صنف مـبدال_أيقوني {
    @حقنة عرف مركب: مـركب؛

    // المتغيرات
    عرف المشاهد: مـصفوفة[سـندنا[مـشهد_أيقوني]]؛
    عرف معالج_تغير_المسار: مغلفة (سند[نـافذة], سند[صـحيح]) = مغلفة (سند[نـافذة], سند[صـحيح]) {
        هذا.عند_تغير_المسار()؛
    };

    عملية هذا~هيئ() {
        نـافذة.النموذج.عند_تغير_المسار.اربط(هذا.معالج_تغير_المسار)؛
        هذا.المشهد = صـندوق({ }).{
            الطراز.{
                الموقع = "relative"؛
                //الإظهار = "flex"؛
                النسق = "row"؛
                العرض = "100%"؛
                الطول = "100%"؛
            }؛
        }؛
        هذا.أضف_مشهدا(مـشهد_أيقوني(نـص("10pt")، نـص("/one")، نـص("/الموارد/معلومات.svg")، مـشهد_المعلومات()))؛
        هذا.أضف_مشهدا(مـشهد_أيقوني(نـص("70pt")، نـص("/two")، نـص("/الموارد/مشاريع.svg")، مـشهد_المعلومات()))؛
        هذا.أضف_مشهدا(مـشهد_أيقوني(نـص("130pt")، نـص("/three")، نـص("/الموارد/وظائف.svg")، مـشهد_المعلومات()))؛
        هذا.أضف_مشهدا(مـشهد_أيقوني(نـص("190pt")، نـص("/four")، نـص("/الموارد/اتصل.svg")، مـشهد_المعلومات()))؛
        هذا.عند_تغير_المسار()؛
    }

    عملية هذا~أتلف() {
        نـافذة.النموذج.عند_تغير_المسار.افصل(هذا.معالج_تغير_المسار)؛
    }

    عملية هذا.أضف_مشهدا(مشهد: سـندنا[مـشهد_أيقوني]) {
        هذا.المشاهد.أضف(مشهد)؛
        هذا.المشهد.كائن~مثل[سند[صـندوق]].أضف_فروع({ مشهد })؛
    }

    عملية هذا.عند_تغير_المسار() {
        عرف ع: صحيح؛
        لكل ع = 0، ع < هذا.المشاهد.هات_الطول()، ++ع {
            إذا نـافذة.النموذج.المسار == هذا.المشاهد(ع).المسار {
                هذا.المشاهد(ع).افتح()؛
            } وإلا {
                هذا.المشاهد(ع).أغلق()؛
            }
        }
    }

    عملية هذا_الصنف (): سـندنا[مـبدال_أيقوني] {
        أرجع سـندنا[مـبدال_أيقوني]().{ احجز()~هيئ() }؛
    }
}

