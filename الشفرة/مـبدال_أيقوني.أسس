صنف مـبدال_أيقوني {
    @حقنة عرف مركب: مـركب؛

    // المتغيرات
    عرف المشاهد: مـصفوفة[سـندنا[صـفحة_أيقونية]]؛
    عرف معالج_تغير_المسار: مغلفة (سند[نـافذة], سند[صـحيح]) = مغلفة (سند[نـافذة], سند[صـحيح]) {
        هذا.عند_تغير_المسار()؛
    };

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        نـافذة.النموذج.عند_تغير_المسار.اربط(هذا.معالج_تغير_المسار)؛
        هذا.المشهد = صـندوق({ }).{
            الطراز.{
                الموقع = "relative"؛
                الإظهار = "flex"؛
                النسق = "column"؛
                ملء_السطر = "start"؛
                المحاذاة = "center"؛
                العرض = "100%"؛
                الطول = "calc(100vh - 10pt)"؛
            }؛
            عند_تغير_الأبعاد.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                عرف ع: صـحيح؛
                لكل ع = 0، ع < الكائن.المشاهد.هات_الطول()، ++ع {
                    الكائن.المشاهد(ع).قد_تغير_العرض(هذا.هات_الأبعاد().عرض)؛
                }
            })؛
            أضف_فروع({
                مـشهد_وثيقة().{
                    الطراز.{
                        الهامش = ربـاعية(60، 0، 0، 0)؛
                        محاذاة_النص = "center"؛
                    }؛
                    حدد_ماركداون(نـص(
                        "برمجتُ هذا الموقع باستخدام [مـنصة_ويب](https://github.com/Alusus/WebPlatform)\\n\ج"
                        "والتي برمجتها باستخدام [لغة الأسس](https://alusus.org)\\n\ج"
                        "والتي هي الأخرى من إنتاجاتي البرمجية"
                    )، 1)؛
                }،
                كـتابة().{
                    النص = نـص("عن الموقع")؛
                    الطراز.{
                        الهامش = ربـاعية(20، 0، 0، 0)؛
                        لون_الخط = السمة.لون_الرابط؛
                        المؤشر = "pointer"؛
                    }؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        نـافذة.النموذج.ادفع_مسارا("/about")؛
                    })؛
                }
            })؛
        }؛
        هذا.أضف_مشهدا(صـفحة_أيقونية(
            نـص("10pt")، نـص("/info")، نـص("/الموارد/معلومات.svg")، نـص("معلومات عامة")، مـشهد_المعلومات()
        ))؛
        هذا.أضف_مشهدا(صـفحة_أيقونية(
            نـص("70pt")، نـص("/projects")، نـص("/الموارد/مشاريع.svg")، نـص("مشاريع")، مـشهد_المشاريع()
        ))؛
        هذا.أضف_مشهدا(صـفحة_أيقونية(
            نـص("130pt")، نـص("/jobs")، نـص("/الموارد/وظائف.svg")، نـص("الخدمة المهنية")، مـشهد_الخبرة_المهنية()
        ))؛
        هذا.أضف_مشهدا(صـفحة_أيقونية(
            نـص("190pt")، نـص("/contact")، نـص("/الموارد/اتصل.svg")، نـص("اتصل بي")، مـشهد_الاتصال()
        ))؛
        هذا.عند_تغير_المسار()؛
    }

    عملية هذا~أتلف() {
        نـافذة.النموذج.عند_تغير_المسار.افصل(هذا.معالج_تغير_المسار)؛
    }

    عملية هذا.أضف_مشهدا(مشهد: سـندنا[صـفحة_أيقونية]) {
        هذا.المشاهد.أضف(مشهد)؛
        هذا.المشهد.كائن~مثل[سند[صـندوق]].أضف_فروع({ مشهد })؛
        مشهد.قد_تغير_العرض(هذا.المشهد.هات_الأبعاد().عرض)؛
    }

    عملية هذا.عند_تغير_المسار() {
        عرف ع: صحيح؛
        لكل ع = 0، ع < هذا.المشاهد.هات_الطول()، ++ع {
            إذا نـص.قارن(نـافذة.النموذج.المسار، هذا.المشاهد(ع).المسار، هذا.المشاهد(ع).المسار.هات_الطول()) == 0 {
                هذا.المشاهد(ع).افتح()؛
            } وإلا {
                هذا.المشاهد(ع).أغلق()؛
            }
        }
    }

    عملية هذا_الصنف (): سـندنا[مـبدال_أيقوني] {
        أرجع سـندنا[مـبدال_أيقوني]().{ احجز()~هيئ() }؛
    }
}

