ماكرو سلسلة_محارف_من_ملف[اسم_الملف] {
    تمهيد {
        عرف نص: نـص = نـم.اقرأ_ملف(اسم_الملف)؛
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(نص))؛
    }
}

ماكرو نص_لتنف_من_ملف_ماركداون[اسم_الملف] {
    تمهيد {
        استخدم الـقلب.أسـاسيات؛
        استخدم الـقلب.بـيانات.شـبم؛
        عرف نص: نـص = نـم.اقرأ_ملف(اسم_الملف)؛
        عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
        مترجم.ألسنة_جديدة_للروابط = 1؛
        نـبم.مدير_شبم.احشر_شبم(
            (شبم نـص(النص))،
            مـتم.تـطبيق[نـص، سند[كـائن_بهوية]]().حدد(نـص("النص")، نـص_حرفي(مترجم.ترجم(نص)))
        )؛
    }
}

ماكرو نصوص_لتنف_من_ملف_ماركداون[اسم_الملف] {
    تمهيد {
        استخدم الـقلب.أسـاسيات؛
        استخدم الـقلب.بـيانات.شـبم؛
        عرف نصوص: مـصفوفة[نـص] = نـم.اقرأ_ملف(اسم_الملف).قطع("---")؛
        عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
        مترجم.ألسنة_جديدة_للروابط = 1؛
        عرف قائمة: سـندنا[قـائمة] = أنشئ_كائنا_مشتركا[قـائمة]؛
        عرف حاوية_القائمة: حـاوية_مرنة[قائمة]؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < نصوص.هات_الطول()، ++ع {
            حاوية_القائمة.أضف_عنصرا(نـبم.مدير_شبم.أنشئ_شبم(
                (شبم نـص(النص))،
                مـتم.تـطبيق[نـص، سند[كـائن_بهوية]]().حدد(نـص("النص")، نـص_حرفي(مترجم.ترجم(نصوص(ع))))
            ))؛
        }
        نـبم.مدير_شبم.احشر_شبم(قائمة)؛
    }
}

ماكرو سلسلة_محارف_من_متغير[المتغير] {
    تمهيد {
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(المتغير))؛
    }
}

دالة الأعلى[صـنف: صنف] (ق1: صـنف، ق2: صـنف): صـنف {
    إذا ق1 >= ق2 أرجع ق1 وإلا أرجع ق2؛
}

دالة اقرأ_رقمًا(محارف: مؤشر[مـحرف]): صـحيح {
    إذا محارف~محتوى < '0' أو محارف~محتوى > '9' أرجع -1؛
    عرف ناتج: صـحيح = 0؛
    بينما محارف~محتوى >= '0' و محارف~محتوى <= '9' {
        ناتج *= 10؛
        ناتج += محارف~محتوى - '0'؛
        محارف = محارف + 1؛
    }
    أرجع ناتج؛
}

دالة رمّز_إكسمل(نص: نـص): نـص {
    أرجع نص.استبدل("\\"، "\\\\").استبدل("\ج"، "\\n").استبدل("\""، "\\\"")؛
}

دالة أرسل_بريدًا_إلكترونيا(البريد_الإلكتروني: نـص، الاسم: نـص، العنوان: نـص، المتن: نـص): ثـنائي {
    عرف جيسون: نـص = نـص.املأ(
        "{"
        ‏"    \"personalizations\":[{"
        ‏"        \"to\":[{\"email\":\"%s\"}],"
    ‏    "        \"subject\":\"رسالة عبر سيرتي من %s: %s\""
        ‏"    }],"
        ‏"    \"content\": [{"
        ‏"        \"type\": \"text/plain\","
        ‏"        \"value\": \"من: %s <%s>\\n\\nالرسالة:\\n%s\""
        ‏"    }],"
        ‏"    \"from\":{"
        ‏"        \"email\":\"%s\""
        ‏"    }"
        ‏"}"،
        إعـدادات.مستلم_البريد_الإلكتروني.صوان،
        رمّز_إكسمل(الاسم).صوان،
        رمّز_إكسمل(العنوان).صوان،
        رمّز_إكسمل(الاسم).صوان،
        رمّز_إكسمل(البريد_الإلكتروني).صوان،
        رمّز_إكسمل(المتن).صوان،
        إعـدادات.مستلم_البريد_الإلكتروني.صوان
    )؛

    عرف معرف_كرل: مؤشر[شـبكة.كـرل] = شـبكة.كـرل_سهل.هيئ()؛
    إذا معرف_كرل == 0 {
        طـرفية.اطبع("فشلت تهيئة كرل\ج")؛
    }؛

    // تهيئة مفتاح SendGrid.
    عرف ترويسة_التوثيق: مصفوفة[مـحرف, 256]؛
    نـص.عيّن(ترويسة_التوثيق~مؤشر، "authorization: Bearer %s"، إعـدادات.مفتاح_سيند_جريد.صوان)؛
    عرف ترويسات: مؤشر[شـبكة.كـرل_سلسلة_محارف] = 0؛
    ترويسات = شـبكة.كـرل_سلسلة_محارف.ألحق(ترويسات، ترويسة_التوثيق~مؤشر)؛
    ترويسات = شـبكة.كـرل_سلسلة_محارف.ألحق(ترويسات، "content-type: application/json")؛

    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._الرابط_، "https://api.sendgrid.com/v3/mail/send")؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._ايداع_بننت_، 1)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حقول_الايداع_، جيسون.صوان)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حجم_حقل_الايداع_، جيسون.هات_الطول())؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حقل_بننت_رأسي_، ترويسات)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._فحص_قرين_طما_، 0)؛

    عرف النتيجة: ثـنائي؛
    إذا شـبكة.كـرل_سهل.أنجز(معرف_كرل) == شـبكة.رمـز_كرل._أوكي_ {
        عرف رمز_الإجابة: صـحيح[64]؛
        شـبكة.كـرل_سهل.هات_معلومة(معرف_كرل، شـبكة.مـعلومات_كرل._رمز_الإجابة_، رمز_الإجابة~مؤشر)؛
        إذا رمز_الإجابة >= 200 و رمز_الإجابة < 300 {
            طـرفية.اطبع("أُرسلت رسالة بريد إلكتروني من %s إلى %s\ج"،
                البريد_الإلكتروني.صوان، إعـدادات.مستلم_البريد_الإلكتروني.صوان
            )؛
            النتيجة = 1؛
        } وإلا {
            طـرفية.اطبع("فشل إرسال البريد الإلكتروني\ج")؛
            النتيجة = 0؛
        }
    } وإلا {
        طـرفية.اطبع("فشل إرسال البريد الإلكتروني\ج")؛
        النتيجة = 0؛
    }؛

    شـبكة.كـرل_سهل.نظف(معرف_كرل)؛
    أرجع النتيجة؛
}

دالة هيئ_المستند(عدد_الوظائف: صـحيح، عدد_المشاريع: صـحيح) {
    // هيئ المجلد.
    نـظام.شغل("mkdir -p /tmp/resume")؛
    نـظام.شغل("cp -r الموارد /tmp/resume/")
    // أنشئ المستند.
    عرف ماركداون: نـص = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/تعريف_للوثيقة.نص") + "\ج\ج" +
        نـم.اقرأ_ملف("الـبيانات/إنـجليزي/المعلومات.نص").استبدل("---"، "")؛
    إذا عدد_الوظائف > 0 {
        ماركداون += "\ج---\ج## Professional Experience\ج\ج"؛
        عرف الوظائف: مـصفوفة[نـص] = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/الخبرة_المهنية.نص")
            .استبدل("/الموارد"، "الموارد")
            .استبدل("## "، "### ")
            .قطع("\ج---\ج")
            .اجتزئ(0، عدد_الوظائف)؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < الوظائف.هات_الطول()، ++ع ماركداون += الوظائف(ع)؛
    }
    إذا عدد_المشاريع > 0 {
        ماركداون += "\ج---\ج## Projects\ج\ج"؛
        عرف المشاريع: مـصفوفة[نـص] = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/المشاريع.نص")
            .استبدل("/الموارد"، "الموارد")
            .استبدل("## "، "### ")
            .قطع("\ج---\ج")
            .اجتزئ(0، عدد_المشاريع)؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < المشاريع.هات_الطول()، ++ع ماركداون += المشاريع(ع)؛
    }
    عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
    عرف طراز:
    "@font-face { font-family: UbuntuArabic; src: url(الموارد/UbuntuArabic-Regular.ttf); }"
    "@font-face { font-family: UbuntuArabicBold; src: url(الموارد/UbuntuArabic-Bold.ttf); }"
    "body { font-size: 11px; font-family: UbuntuArabic }"
    "h1 { font-size: 19px; font-family: UbuntuArabicBold }"
    "h2 { font-size: 16px; font-family: UbuntuArabicBold }"
    "h3 { font-size: 13px; font-family: UbuntuArabicBold }"
    "img { display: none; }"؛
    عرف قالب: "<html><head><style>%s</style><title>Sarmad Khalid Abdullah</title></head><body>%s</body></html>"؛
    عرف ناتج: نـص = نـص.املأ(قالب، طراز، مترجم.ترجم(ماركداون).صوان)؛
    نـم.أنشئ_ملف("/tmp/resume/sarmad-abdullah-resume.html"، ناتج، ناتج.هات_الطول())؛
    // حول المستند إلى صيغة pdf.
    نـظام.شغل(نـص.املأ("mkdir -p %s"، مسار_المستند.صوان))؛
    نـظام.شغل(
        نـص.املأ("weasyprint /tmp/resume/sarmad-abdullah-resume.html %s/sarmad-abdullah-resume.pdf"، مسار_المستند.صوان)
    )؛
}

دالة ابن {
    هيئ_المستند(20، 7)؛
    نـظام.شغل("cp -r الموارد Build/")؛
    عرف تنفيذي: بـناء.تـنفيذي(مدخل~شبم، "Build/seeraty")؛
    تنفيذي.أضف_اعتماديات(مـنصة_ويب.هات_اعتماديات_البناء())؛
    تنفيذي.أضف_اعتماديات(شـبكة.هات_اعتماديات_البناء())؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        نـظام.فشل(1، "فشل البناء")؛
    }؛
}

دالة انشر {
    // تفحص الإعدادات.
    إذا إعـدادات.منفذ_النشر == "" أو
        إعـدادات.اسم_المستخدم_للنشر == "" أو
        إعـدادات.عنوان_الخادم_للنشر == "" أو
        إعـدادات.مسار_النشر == "" {
            نـظام.فشل(1، "إعدادات النشر غير مضبوطة. يرجى ضبط المتغيرات المحيطية التالية:\ج"
                ‏"SEERATY_PUBLISH_USERNAME\ج"
                ‏"SEERATY_PUBLISH_HOST\ج"
                ‏"SEERATY_PUBLISH_PORT\ج"
                ‏"SEERATY_PUBLISH_PATH\ج"
                "بالإضافة للمتغيرات المحيطية يجب ضبط مفتاح SSH في الخادم\ج"
            )؛
        }
    // تحميل البرنامج للخادم.
    عرف أمر: نـص = نـص.املأ("rsync -avhr -e 'ssh -p %s' ./Build/* %s@%s:%s"،
        إعـدادات.منفذ_النشر.صوان،
        إعـدادات.اسم_المستخدم_للنشر.صوان،
        إعـدادات.عنوان_الخادم_للنشر.صوان،
        إعـدادات.مسار_النشر.صوان
    )؛
    إذا نـظام.شغل(أمر) != 0 {
        نـظام.فشل(1، "فشل النشر: فشل تحميل البرنامج للخادم.")؛
    }
    طـرفية.اطبع("حُمل البرنامج للخادم.\ج")؛
    // إعادة تشغيل الخادم.
    أمر = نـص.املأ("ssh -p %s %s@%s 'systemctl restart seeraty'"،
        إعـدادات.منفذ_النشر.صوان،
        إعـدادات.اسم_المستخدم_للنشر.صوان،
        إعـدادات.عنوان_الخادم_للنشر.صوان
    )؛
    إذا نـظام.شغل(أمر) != 0 {
        نـظام.فشل(1، "فشل النشر: فشلت إعادة تشغيل الخادم.")؛
    }
    طـرفية.اطبع("أعيد تشغيل الخادم.\ج")؛
    طـرفية.اطبع("تم النشر بنجاح.\ج")؛
}
