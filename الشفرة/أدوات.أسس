ماكرو سلسلة_محارف_من_ملف[اسم_الملف] {
    تمهيد {
        عرف نص: نـص = نـم.اقرأ_ملف(اسم_الملف)؛
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(نص))؛
    }
}

ماكرو نص_لتنف_من_ملف_ماركداون[اسم_الملف] {
    تمهيد {
        استخدم الـقلب.أسـاسيات؛
        استخدم الـقلب.بـيانات.شـبم؛
        عرف نص: نـص = نـم.اقرأ_ملف(اسم_الملف)؛
        عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
        مترجم.ألسنة_جديدة_للروابط = 1؛
        نـبم.مدير_شبم.احشر_شبم(
            (شبم نـص(النص))،
            مـتم.تـطبيق[نـص، سند[كـائن_بهوية]]().حدد(نـص("النص")، نـص_حرفي(مترجم.ترجم(نص)))
        )؛
    }
}

ماكرو نصوص_لتنف_من_ملف_ماركداون[اسم_الملف] {
    تمهيد {
        استخدم الـقلب.أسـاسيات؛
        استخدم الـقلب.بـيانات.شـبم؛
        عرف نصوص: مـصفوفة[نـص] = نـم.اقرأ_ملف(اسم_الملف).قطع("---")؛
        عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
        مترجم.ألسنة_جديدة_للروابط = 1؛
        عرف قائمة: سـندنا[قـائمة] = أنشئ_كائنا_مشتركا[قـائمة]؛
        عرف حاوية_القائمة: حـاوية_مرنة[قائمة]؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < نصوص.هات_الطول()، ++ع {
            حاوية_القائمة.أضف_عنصرا(نـبم.مدير_شبم.أنشئ_شبم(
                (شبم نـص(النص))،
                مـتم.تـطبيق[نـص، سند[كـائن_بهوية]]().حدد(نـص("النص")، نـص_حرفي(مترجم.ترجم(نصوص(ع))))
            ))؛
        }
        نـبم.مدير_شبم.احشر_شبم(قائمة)؛
    }
}

ماكرو سلسلة_محارف_من_متغير[المتغير] {
    تمهيد {
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(المتغير))؛
    }
}

دالة الأعلى[صـنف: صنف] (ق1: صـنف، ق2: صـنف): صـنف {
    إذا ق1 >= ق2 أرجع ق1 وإلا أرجع ق2؛
}

دالة اقرأ_رقمًا(محارف: مؤشر[مـحرف]): صـحيح {
    إذا محارف~محتوى < '0' أو محارف~محتوى > '9' أرجع -1؛
    عرف ناتج: صـحيح = 0؛
    بينما محارف~محتوى >= '0' و محارف~محتوى <= '9' {
        ناتج *= 10؛
        ناتج += محارف~محتوى - '0'؛
        محارف = محارف + 1؛
    }
    أرجع ناتج؛
}

دالة رمّز_إكسمل(نص: نـص): نـص {
    أرجع نص.استبدل("\\"، "\\\\").استبدل("\ج"، "\\n").استبدل("\""، "\\\"")؛
}

دالة أرسل_بريدًا_إلكترونيا(البريد_الإلكتروني: نـص، الاسم: نـص، العنوان: نـص، المتن: نـص): ثـنائي {
    عرف جيسون: نـص = نـص.املأ(
        "{"
        ‏"    \"personalizations\":[{"
        ‏"        \"to\":[{\"email\":\"%s\"}],"
    ‏    "        \"subject\":\"رسالة عبر سيرتي من %s: %s\""
        ‏"    }],"
        ‏"    \"content\": [{"
        ‏"        \"type\": \"text/plain\","
        ‏"        \"value\": \"من: %s <%s>\\n\\nالرسالة:\\n%s\""
        ‏"    }],"
        ‏"    \"from\":{"
        ‏"        \"email\":\"%s\""
        ‏"    }"
        ‏"}"،
        إعـدادات.مستلم_البريد_الإلكتروني.صوان،
        رمّز_إكسمل(الاسم).صوان،
        رمّز_إكسمل(العنوان).صوان،
        رمّز_إكسمل(الاسم).صوان،
        رمّز_إكسمل(البريد_الإلكتروني).صوان،
        رمّز_إكسمل(المتن).صوان،
        إعـدادات.مستلم_البريد_الإلكتروني.صوان
    )؛

    عرف معرف_كرل: مؤشر[شـبكة.كـرل] = شـبكة.كـرل_سهل.هيئ()؛
    إذا معرف_كرل == 0 {
        طـرفية.اطبع("فشلت تهيئة كرل\ج")؛
    }؛

    // تهيئة مفتاح SendGrid.
    عرف ترويسة_التوثيق: مصفوفة[مـحرف, 256]؛
    نـص.عيّن(ترويسة_التوثيق~مؤشر، "authorization: Bearer %s"، إعـدادات.مفتاح_سيند_جريد.صوان)؛
    عرف ترويسات: مؤشر[شـبكة.كـرل_سلسلة_محارف] = 0؛
    ترويسات = شـبكة.كـرل_سلسلة_محارف.ألحق(ترويسات، ترويسة_التوثيق~مؤشر)؛
    ترويسات = شـبكة.كـرل_سلسلة_محارف.ألحق(ترويسات، "content-type: application/json")؛

    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._الرابط_، "https://api.sendgrid.com/v3/mail/send")؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._ايداع_بننت_، 1)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حقول_الايداع_، جيسون.صوان)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حجم_حقل_الايداع_، جيسون.هات_الطول())؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._حقل_بننت_رأسي_، ترويسات)؛
    شـبكة.كـرل_سهل.اضبط_خيار(معرف_كرل، شـبكة.خـيارات_كرل._فحص_قرين_طما_، 0)؛

    عرف النتيجة: ثـنائي؛
    إذا شـبكة.كـرل_سهل.أنجز(معرف_كرل) == شـبكة.رمـز_كرل._أوكي_ {
        عرف رمز_الإجابة: صـحيح[64]؛
        شـبكة.كـرل_سهل.هات_معلومة(معرف_كرل، شـبكة.مـعلومات_كرل._رمز_الإجابة_، رمز_الإجابة~مؤشر)؛
        إذا رمز_الإجابة >= 200 و رمز_الإجابة < 300 {
            طـرفية.اطبع("أُرسلت رسالة بريد إلكتروني من %s إلى %s\ج"،
                البريد_الإلكتروني.صوان، إعـدادات.مستلم_البريد_الإلكتروني.صوان
            )؛
            النتيجة = 1؛
        } وإلا {
            طـرفية.اطبع("فشل إرسال البريد الإلكتروني\ج")؛
            النتيجة = 0؛
        }
    } وإلا {
        طـرفية.اطبع("فشل إرسال البريد الإلكتروني\ج")؛
        النتيجة = 0؛
    }؛

    شـبكة.كـرل_سهل.نظف(معرف_كرل)؛
    أرجع النتيجة؛
}

دالة هيئ_المستند(عدد_الوظائف: صـحيح، عدد_المشاريع: صـحيح) {
    // هيئ المجلد.
    نـظام.شغل("mkdir -p /tmp/seeraty/resume")؛
    نـظام.شغل("cp -r الموارد /tmp/seeraty/resume/")
    // أنشئ المستند.
    عرف عمودان:
    "<div style='width:100%%;display:flex;flex-direction:row;'>"
    "<div style='flex: 0.5;'>"
    "%s"
    "</div>"
    "<div style='padding-left:20px;flex:0.5;'>"
    "%s"
    "</div>"
    "</div>";
    عرف عمودان_بفاصل:
    "<div style='width:100%%;display:flex;flex-direction:row;border-bottom:2px solid;'>"
    "<div style='flex: 0.5;'>"
    "%s"
    "</div>"
    "<div style='padding-left:10px;margin-left:10px;border-left:2px solid;flex:0.5;'>"
    "%s"
    "</div>"
    "</div>";
    عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
    عرف المعلومات: مـصفوفة[نـص] = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/المعلومات.نص").قطع("\ج---\ج")؛
    عرف المتن: نـص = نـص.املأ(
        عمودان_بفاصل، مترجم.ترجم(نـم.اقرأ_ملف("الـبيانات/إنـجليزي/تعريف_للوثيقة.نص")).صوان،
        مترجم.ترجم(المعلومات(0)).صوان
    )؛
    إذا عدد_الوظائف > 0 {
        المتن += "\ج<h2>Professional Experience</h2>\ج\ج"؛
        عرف الوظائف: مـصفوفة[نـص] = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/الخبرة_المهنية.نص")
            .استبدل("/الموارد"، "الموارد")
            .استبدل("## "، "### ")
            .قطع("\ج---\ج")
            .اجتزئ(0، عدد_الوظائف)؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < الوظائف.هات_الطول()، ++ع المتن += مترجم.ترجم(الوظائف(ع))؛
    }
    إذا عدد_المشاريع > 0 {
        المتن += "\ج<h2>Open Source Projects</h2>\ج\ج"؛
        عرف المشاريع: مـصفوفة[نـص] = نـم.اقرأ_ملف("الـبيانات/إنـجليزي/المشاريع.نص")
            .استبدل("/الموارد"، "الموارد")
            .استبدل("## "، "### ")
            .قطع("\ج---\ج")
            .اجتزئ(0، عدد_المشاريع)؛
        عرف منتصف: صـحيح = المشاريع.هات_الطول() ÷ 2؛
        عرف عمود1: نـص؛
        عرف عمود2: نـص؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < منتصف، ++ع عمود1 += مترجم.ترجم(المشاريع(ع))؛
        لكل ع = منتصف، ع < المشاريع.هات_الطول()، ++ع عمود2 += مترجم.ترجم(المشاريع(ع))؛
        المتن += نـص.املأ(عمودان، عمود1.صوان، عمود2.صوان)؛
    }
    المتن += نـص.املأ(عمودان، مترجم.ترجم(المعلومات(1)).صوان، مترجم.ترجم(المعلومات(2)).صوان)؛
    المتن += نـص.املأ(
        عمودان،
        (مترجم.ترجم(المعلومات(3)) + مترجم.ترجم(المعلومات(4))).صوان،
        مترجم.ترجم(المعلومات(5)).صوان
    )؛
    عرف طراز:
    "@page { margin:1cm; size:letter; }"
    "@font-face { font-family: UbuntuArabic; src: url(الموارد/UbuntuArabic-Regular.ttf); }"
    "@font-face { font-family: UbuntuArabicBold; src: url(الموارد/UbuntuArabic-Bold.ttf); }"
    "body { font-size: 12px; font-family: UbuntuArabic }"
    "h1 { font-size: 20px; font-family: UbuntuArabicBold }"
    "h2 { font-size: 17px; font-family: UbuntuArabicBold }"
    "h3 { font-size: 14px; font-family: UbuntuArabicBold }"
    "img { display: none; }"؛
    عرف قالب: "<html><head><style>%s</style><title>Sarmad Khalid Abdullah</title></head><body>%s</body></html>"؛
    عرف ناتج: نـص = نـص.املأ(قالب، طراز، المتن.صوان)؛
    نـم.أنشئ_ملف("/tmp/seeraty/resume/sarmad-abdullah-resume.html"، ناتج، ناتج.هات_الطول())؛
    // حول المستند إلى صيغة pdf.
    طـرفية.اطبع("إنشاء ملف السيرة الذاتية...\ج")؛
    نـظام.شغل("weasyprint /tmp/seeraty/resume/sarmad-abdullah-resume.html /tmp/seeraty/resume/sarmad-abdullah-resume.pdf")؛
    طـرفية.اطبع("اكتمل إنشاء ملف السيرة الذاتية\ج")؛
}
