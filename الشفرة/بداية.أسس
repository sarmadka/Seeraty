اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
مـحا.اشمل_ملف("Alusus/Recaptcha"، "ريـكابجا.أسس")؛
مـحا.اشمل_ملف("Alusus/MarkdownTranslator"، "مـترجم_ماركداون.أسس")؛
مـحا.اشمل_ملف("Alusus/I18n"، "تـرجمة.أسس")؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/تـطبيق"؛
اشمل "مـتم/نـم"؛
اشمل "مـتم/نـمط"؛
اشمل "مـتم/شـبكة"؛
اشمل "مـتم/نـظام"؛
اشمل "الـقلب/بـيانات"؛
اشمل "نـبم"؛
استخدم مـتم؛
استخدم مـنصة_ويب؛

اشمل "أدوات"؛
اشمل "مـركبات/كـتابة_عنوان"؛
اشمل "مـركبات/كـتابة_متن"؛
اشمل "مـركبات/قـابل_للضغط"؛
اشمل "مـركبات/عـمود_نصوص"؛
اشمل "مـركبات/صـفحة_أيقونية"؛
اشمل "مـبدال_أيقوني"؛
اشمل "مـشاهد/الـترويسة"؛
اشمل "مـشاهد/مـشهد_أيقوني"؛
اشمل "مـشاهد/مـشهد_المعلومات"؛
اشمل "مـشاهد/مـشهد_المشاريع"؛
اشمل "مـشاهد/مـشهد_الخط_الزمني"؛
اشمل "مـشاهد/مـشهد_الخبرة_المهنية"؛
اشمل "مـشاهد/مـشهد_الاتصال"؛
اشمل "إعـدادات"؛
اشمل "بـيانات"؛
اشمل "سمات"؛

//==============================================================================
// الخادم

@مسار_موارد عرف مسار_الموارد: "الموارد/"؛

//==============================================================================
// صـفحات واجهة المستخدم

عرف اللغة: نـص؛
عرف من_اليمين: ثـنائي؛
عرف نمط_الألوان: نـص؛

@منفذ_مرئي["/*", "Sarmad Abdullah"]
دالة رئيسي {
    // تهيئات
    انتظر(ريـكابجا.هيئ_الواجهة())؛
    اللغة = نـافذة.النموذج.المخزن_المحلي.هات_عنصرا("اللغة")؛
    إذا اللغة == "" اللغة = هات_اللغة_المفضلة(مـصفوفة[نـص]({ نـص("en")، نـص("ar") }))؛
    إذا اللغة == "ar" من_اليمين = 1 وإلا من_اليمين = 0؛
    هيئ_البيانات(اللغة)؛
    نمط_الألوان = نـافذة.النموذج.المخزن_المحلي.هات_عنصرا("نمط_الألوان")؛
    إذا نمط_الألوان == "" {
        إذا هل_يفضل_نمط_الألوان_الداكن() نمط_الألوان = "داكن" وإلا نمط_الألوان = "فاتح"؛
    }
    هيئ_السمات(نمط_الألوان)؛
    تـرجمة.هيئ(البيانات.نصوص)؛

    // تضبيطات الطرز
    حمل_خط("UbuntuArabic", "/الموارد/UbuntuArabic-Regular.ttf")؛
    أخف_أشرطة_التمرير_في_كروم()؛
    نـافذة.النموذج.{
        الطراز.{
            الحشوة = مـسافة4.نقاط(0)؛
            الهامش = مـسافة4.نقاط(0)؛
            تقطيع_الكلمات = تـقطيع_كلمات._قطع_الكلمات_؛
            فئة_الخط = "UbuntuArabic"؛
            إذا من_اليمين الاتجاه = اتـجاه._من_اليمين_؛
        }؛
        الطراز({ ">>$a"، مـحدد_حالة._رابط_ }).{
            لون_الخط = السمة.لون_الرابط؛
            زخرفة_النص = زخـرفة_نص._بلا_؛
        }؛
        الطراز({ ">>$a"، مـحدد_حالة._مزار_ }).{
            لون_الخط = السمة.لون_الرابط؛
            زخرفة_النص = زخـرفة_نص._بلا_؛
        }؛
    }؛

    // إنشاء المشهد
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            العرض = مـسافة.مئوي(100)؛
            الطول = مـسافة.مئوي(100)؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
            المحاذاة = مـحاذاة._وسط_؛
            الخلفية = خـلفية(السمة.لون_الهامش)؛
            لون_الخط = السمة.لون_الكتابة؛
        }؛
        أضف_فروع({
            مـكداس_توجيه().{
                الطراز.{
                    العرض = مـسافة.مئوي(100)؛
                    أقصى_عرض = مـسافة.نقاط(800)؛
                    الطول = مـسافة.مئوي(100)؛
                    الخلفية = خـلفية(السمة.لون_الخلفية_الرئيسية)؛
                }؛
                حدد_الانتقال(
                    أنشئ_انتقال_انزلاق_مكداس(0.5, 1, 0)،
                    أنشئ_انتقال_انزلاق_مكداس(0.5, 1, 1)
                )؛
                مسار(نـص("^/")) = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                    أرجع صـندوق().{
                        الطراز.{
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.مئوي(100)؛
                            الفيض_ص = فـيض._تمرير_؛
                        }؛
                        أضف_فروع({
                            الـترويسة(),
                            مـبدال_أيقوني()
                        })؛
                    }؛
                }؛
                مسار(نـص("^/about")) = مغلفة(حـمولة_مسار): سـندنا[ودجـة] {
                    أرجع صـندوق().{
                        الطراز.{
                            العرض = مـسافة.مئوي(100) - مـسافة.نقاط(20)؛
                            الطول = مـسافة.مئوي(100) - مـسافة.نقاط(20)؛
                            الفيض_ص = فـيض._تمرير_؛
                            الإظهار = إظـهار._مرن_؛
                            النسق = نـسق._عمود_؛
                            المحاذاة = مـحاذاة._وسط_؛
                            الحشوة = مـسافة4.نقاط(10)؛
                            الخلفية = خـلفية(السمة.لون_خلفية_الصفحة)؛
                        }؛
                        أضف_فروع({
                            مـشهد_وثيقة().{
                                حدد_ماركداون(البيانات.عن_الموقع، 1)؛
                            }،
                            كـتابة().{
                                النص = تـرجمة.نص("أغــلق")؛
                                الطراز.{
                                    الهامش = مـسافة4.نقاط(20، 0، 0، 0)؛
                                    لون_الخط = السمة.لون_الرابط؛
                                    المؤشر = مـؤشر._سبابة_؛
                                }؛
                                عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                                    نـافذة.النموذج.ادفع_مسارا("/")؛
                                })؛
                            }
                        })؛
                    }؛
                }؛
            }
        })؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}

دالة أخف_أشرطة_التمرير_في_كروم {
    أنشئ_عنصر("style"، "hidechromescrollbars"، "head")؛
    حدد_متن_طراز("hidechromescrollbars"، "div::-webkit-scrollbar"، "display: none")؛
}

إذا الـعملية.عدد_المعطيات == 3 {
    عرف المعطى: مـؤشر_محارف = الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1)؛
    إذا نـص.أمتطابق(المعطى، "انشر") {
        انشر()؛
    } وإلا {
        طـرفية.اطبع(
            "خطأ: معطى غير صالح\ج"
            "الاستخدام:\ج"
            "  الأسس الشفرة/بداية.أسس [انشر]\ج"
        )؛
    }
} وإلا {
    هيئ_مسار_المستند(20، 7)؛
    ريـكابجا.هيئ_الخادم()؛
    طـرفية.اطبع("تشغيل الخادم على المنفذ 8000...\nURL: http://localhost:8000/\n")؛
    ابدأ_الخادم({ "listening_ports"، "8000"، "static_file_max_age"، "0" })؛
    طـرفية.اطبع("الخادم جاهز\ج")؛
    بينما 1 نـظام.نم(100000)؛
}
