صنف مـشهد_الخط_الزمني {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(بيانات: مـصفوفة[نـقطة_زمنية]) {
        عرف ألوان: مـصفوفة[نـص]({
            نـص("#888")،
            نـص("#8ff")،
            نـص("#f8f")،
            نـص("#ff8")،
        })؛
        هذا.المشهد = صـندوق().{
            عرف ع: صـحيح؛
            لكل ع = 0، ع < بيانات.هات_الطول()، ++ع {
                عرف طول: صـحيح؛
                إذا ع > 0 و بيانات(ع).هات_البعد_عن(بيانات(ع - 1)) > 0 {
                    أضف_فروع({
                        صـندوق().{
                            الطراز.الطول = نـص.املأ("%ipt"، بيانات(ع).هات_البعد_عن(بيانات(ع - 1)) * 4)؛
                        }
                    })؛
                }
                أضف_فروع({
                    صـندوق().{
                        الطراز.{
                            الإظهار = "flex"؛
                            النسق = "row"؛
                            العرض = "100%"؛
                            الطول = نـص.املأ("%ipt"، الأعلى[صـحيح](بيانات(ع).هات_الطول() * 4، 60))؛
                        }؛
                        أضف_فروع({
                            صـندوق().{
                                الطراز.{
                                    الطول = "100%"؛
                                    العرض = "10pt"؛
                                    الخلفية = ألوان(ع%4)؛
                                    سمك_الإطار = ربـاعية(2، 0، 2، 0)؛
                                    لون_الإطار = "000"؛
                                    طراز_الإطار = "solid"؛
                                }؛
                            }،
                            صـندوق().{
                                الطراز.{
                                    الإظهار = "flex"؛
                                    النسق = "column"؛
                                    ملء_السطر = "space-between"؛
                                    الحشوة = ربـاعية(0، 5، 0، 5)؛
                                }؛
                                أضف_فروع({
                                    كـتابة().{
                                        النص = نـص.املأ("%i-%i"، بيانات(ع).إلى_سنة، بيانات(ع).إلى_شهر)؛
                                        الطراز.حجم_الخط = 8؛
                                    }،
                                    كـتابة().{
                                        النص = بيانات(ع).وصف؛
                                        الطراز.{
                                            حجم_الخط = 12؛
                                            المؤشر = "pointer"؛
                                        }؛
                                        عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                                            نـافذة.النموذج.ادفع_مسارا(نـافذة.النموذج.المسار + "#" + ع)؛
                                        })؛
                                    }،
                                    كـتابة().{
                                        النص = نـص.املأ("%i-%i"، بيانات(ع).من_سنة، بيانات(ع).من_شهر)؛
                                        الطراز.حجم_الخط = 8؛
                                    }،
                                })؛
                            }
                        })؛
                    }
                })؛
            }
        }؛
    }

    عملية هذا_الصنف (بيانات: مـصفوفة[نـقطة_زمنية]): سـندنا[مـشهد_الخط_الزمني] {
        أرجع سـندنا[مـشهد_الخط_الزمني]().{ احجز()~هيئ(بيانات) }؛
    }
}

صنف نـقطة_زمنية {
    عرف من_سنة: صـحيح؛
    عرف من_شهر: صـحيح؛
    عرف إلى_سنة: صـحيح؛
    عرف إلى_شهر: صـحيح؛
    عرف وصف: نـص؛
    عملية هذا~هيئ() {}
    عملية هذا~هيئ(من_سنة: صـحيح، من_شهر: صـحيح، إلى_سنة: صـحيح، إلى_شهر: صـحيح، وصف: نـص) {
        هذا.من_سنة = من_سنة؛
        هذا.من_شهر = من_شهر؛
        هذا.إلى_سنة = إلى_سنة؛
        هذا.إلى_شهر = إلى_شهر؛
        هذا.وصف = وصف؛
    }
    عملية هذا~هيئ(سند[هذا_الصنف]) هذا = قيمة؛
    عملية هذا = سند[هذا_الصنف] {
        هذا.من_سنة = قيمة.من_سنة؛
        هذا.من_شهر = قيمة.من_شهر؛
        هذا.إلى_سنة = قيمة.إلى_سنة؛
        هذا.إلى_شهر = قيمة.إلى_شهر؛
        هذا.وصف = قيمة.وصف؛
    }
    عملية هذا.هات_الطول(): صـحيح {
        أرجع هذا.إلى_سنة * 12 + هذا.إلى_شهر - هذا.من_سنة * 12 - هذا.من_شهر؛
    }
    عملية هذا.هات_البعد_عن(سند[هذا_الصنف]): صـحيح {
        أرجع قيمة.من_سنة * 12 + قيمة.من_شهر - هذا.إلى_سنة * 12 - هذا.إلى_شهر؛
    }
}
